apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
android {
  compileSdkVersion 28

  defaultConfig {
    applicationId 'com.example'

    minSdkVersion 21
    targetSdkVersion 28

    versionCode 1
    versionName '1'
  }

  compileOptions {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
  }

  productFlavors {
    flavorDimensions 'backend'

    reflect {
      dimension 'backend'
    }
    codegen {
      dimension 'backend'
    }
  }

  variantFilter {
    // Behavior is varied by flavor so a second build type is useless.
    setIgnore(it.buildType.name == 'release')
  }

  testOptions {
    unitTests {
      // Required for Robolectric.
      includeAndroidResources = true
    }
  }
}

dependencies {
  implementation deps.dagger.runtime
  implementation deps.dagger.androidRuntime
  implementation deps.dagger.androidSupportRuntime
  implementation 'androidx.appcompat:appcompat:1.0.0'
  implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.3.30"
  reflectImplementation project(':reflect')
  kaptReflect project(':reflect-compiler')

  codegenImplementation project(':codegen')
  kaptCodegen deps.dagger.compiler
  kaptCodegen deps.dagger.androidCompiler

  testImplementation deps.junit
  testImplementation deps.truth
  testImplementation deps.robolectric
  testImplementation deps.androidx.test.core
}
